/* =========================================== *
 *                                             *
 * Mod:                 The Jerry Zinger Show  *
 * File:                jz.tp2                 *
 * Responsible authors: SConrad, Shed          *
 *                                             *
 * =========================================== */

BACKUP ~jz/backup~

/* Author
AUTHOR ~
	SConrad (sconrad@spellholdstudios.net)
	Shed 	(shed@spellholdstudios.net)~*/
SUPPORT ~SHS Staff at http://www.shsforums.net/forum/200-jerry-zinger-show~

VERSION ~v5.0~

README ~jz/readme/jz-readme-english.html~

ALWAYS

	/* ---------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE           *
	 *    No exception, everything needs to be converted    *
	 * ---------------------------------------------------- */
	ACTION_DEFINE_ARRAY jznoconvert BEGIN END
	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY jzreload BEGIN jz END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/translations~
			noconvert_array = jznoconvert
			reload_array = jzreload
	END

END


// Languages
LANGUAGE ~English~
         ~english~
         ~jz/translations/english/jz.tra~
         
LANGUAGE ~Deutsch (Uebersetzung von Fey)~
         ~deutsch~
         ~jz/translations/deutsch/jz.tra~

LANGUAGE ~Russian (translation by Austin & aerie.ru. Updated by Prowler)~
         ~russian~
         ~jz/translations/russian/jz.tra~

LANGUAGE ~Italiano (traduzione di al17)~
         ~italian~
         ~jz/translations/italian/jz.tra~


/* ============================================= *
 *    The Jerry Zinger Show  (main component)    *
 * ============================================= */

BEGIN @0                                          // ~The Jerry Zinger Show~
LABEL ~jz_main~
REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @25 // ~Throne of Bhaal required~

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* =============================== *
 *  Remove Delay Between Episodes  *
// =============================== */

BEGIN @19                          // ~Remove Delay Between Episodes~
LABEL ~jz_remove_delay_between_episodes~
//REQUIRE_COMPONENT ~jz.tp2~ ~0~ @22 // ~You must have the Jerry Zinger Show installed.~
//REQUIRE_PREDICATE (MOD_IS_INSTALLED ~jz.tp2~ 0) @22
REQUIRE_PREDICATE MOD_IS_INSTALLED ~jz.tp2~ (ID_OF_LABEL ~jz.tp2~ ~jz_main~) @22 // ~You must have the Jerry Zinger Show installed.~

SILENT
COPY_EXISTING ~jzcount.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~GlobalTimerExpired("JZShow","GLOBAL")~ ~~
		REPLACE_TEXTUALLY ~!GlobalTimerNotExpired("JZShow","GLOBAL")~ ~~
		REPLACE_TEXTUALLY ~GlobalTimerNotExpired("JZShow","GLOBAL")~ ~~
	END
BUT_ONLY

COPY_EXISTING ~jzcount.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~GlobalTimerExpired("JZShow","GLOBAL")~ ~~
		REPLACE_TEXTUALLY ~!GlobalTimerNotExpired("JZShow","GLOBAL")~ ~~
		REPLACE_TEXTUALLY ~GlobalTimerNotExpired("JZShow","GLOBAL")~ ~~
	END
BUT_ONLY
